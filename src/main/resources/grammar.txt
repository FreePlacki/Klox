program        -> declaration* EOF ;

declaration    -> varDecl | statement ;

varDecl        -> "var" IDENTIFIER ( "=" expression )? ";" ;

statement      -> exprStmt
                | ifStmt
                | whileStmt
                | forStmt
                | printStmt
                | block;

exprStmt       -> expression ";" ;

ifStmt         -> "if" "(" expression ")" statement
                  ( "else" statement )? ;

whileStmt      -> "while" "(" expression ")" statement ;

forStmt        -> "for" "(" varDecl | exprStmt ";"
                            expression? ";"
                            expression?
                        ")" statement ;

printStmt      -> "print" expression ";" ;

block          -> "{" declaration* "}" ;

expression     -> assignment ;

assignment     -> IDENTIFIER "=" assignment | ternary ;

ternary        -> logicOr | ( "?" expression ":" ternary ) ;

logicOr        -> logicAnd ( "or" logicAnd )* ;

logicAnd       -> equality ( "and" equality )* ;

equality       -> comparison ( ( "!=" | "==" ) comparison )* ;

comparison     -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;

term           -> factor ( ( "-" | "+" ) factor )* ;

factor         -> unary ( ( "/" | "*" ) unary )* ;

unary          -> ( "!" | "-" ) unary
               | primary ;

primary        -> NUMBER | STRING | "true" | "false" | "nil"
               | "(" expression ")"
               | IDENTIFIER ;
               